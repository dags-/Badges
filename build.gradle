plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'java'
    id 'maven'
}

group 'me.dags'
version '1.2.0-SNAPSHOT'
def spongeAPI = '5.1.0'
def spongeChannel = 'SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-${spongeChannel}"
    compile 'com.github.dags-:CommandBus:1.8.0-RC1'
    compile 'com.github.dags-:TextMU:0.1'
}

shadowJar {
    dependencies {
        include dependency('com.github.dags-:CommandBus')
        include dependency('com.github.dags-:TextMU')
    }
    relocate 'me.dags.commandbus', 'me.dags.badges.commandbus'
    relocate 'me.dags.textmu', 'me.dags.badges.textmu'
    classifier = null
}

install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll {it.scope == "compile"}
}

jar.dependsOn shadowJar
jar.enabled = false